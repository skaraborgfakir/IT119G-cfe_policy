#
# Time-stamp: <2019-03-21 18:19:48 nsa>
#

bundle agent bind
{
  meta:
      "tags" slist => { "autorun" };

  vars:
    !any.disable_DNS_server::
      "prefix"
	string => "/var/cfengine/testings";

    any::
      "prefix"
	string => "",
	unless => isvariable("prefix");

      "template_data"
	data => readjson("$(this.promise_dirname)/hosts.json");

      "controlled_files"
	slist => { "/etc/bind/named.conf",
		   "/etc/bind/named.conf.local",
		   "/etc/bind/named.conf.options",
		   "/etc/bind/named.conf.default-zones"};

  files:
    DNS_server::
      "$(prefix)/."
	create => "true";

    DNS_server::
      "$(controlled_files).saved_dist_version"
	unless => fileexists("$(controlled_files).saved_dist_version"),
	copy_from => local_dcp("$(controlled_files)"),
	comment => "save dist versionen";

      "$(controlled_files).saved_dist_version"
	perms => m("444");

      #
      # named.conf
      #
      "$(prefix)/etc/bind/named.conf.tmpl.mustache"
	create => "true",
	edit_defaults => empty,
	edit_line => named_conf,
	meta => { "bind" };

      "$(prefix)/etc/bind/named.conf"
	create => "true",
	edit_defaults => empty,
	template_method => "mustache",
	edit_template => "$(prefix)/etc/bind/named.conf.tmpl.mustache",
	classes => results("bundle", "bind9_config"),
	meta => { "bind" };

      #
      # named.conf.options
      #
      "$(prefix)/etc/bind/named.conf.options.tmpl.mustache"
	create => "true",
	edit_defaults => empty,
	edit_line => named_conf_options,
	meta => { "bind" };

      "$(prefix)/etc/bind/named.conf.options"
	create => "true",
	edit_defaults => empty,
	template_method => "mustache",
	edit_template => "$(prefix)/etc/bind/named.conf.options.tmpl.mustache",
	classes => results("bundle", "bind9_config"),
	meta => { "bind" };

      #
      # named.conf.local
      #
      "$(prefix)/etc/bind/named.conf.local.tmpl.mustache"
	create => "true",
	edit_defaults => empty,
	edit_line => named_conf_local,
	meta => { "bind" };

      "$(prefix)/etc/bind/named.conf.local"
	create => "true",
	edit_defaults => empty,
	template_method => "mustache",
	edit_template => "$(prefix)/etc/bind/named.conf.local.tmpl.mustache",
	classes => results("bundle", "bind9_config"),
	meta => { "bind" };

      #
      # named.conf.local
      #
      "/etc/bind/zones/db.b18steni.tmpl.mustache"
	create => "true",
	edit_defaults => empty,
	edit_line => b18steni_zon;

      "/etc/bind/zones/db.b18steni"
	create => "true",
	edit_defaults => empty,
	template_method => "mustache",
	edit_template => "/etc/bind/zones/db.b18steni.tmpl.mustache",
	classes => results("bundle", "bind9_zone_data"),
	meta => { "bind" };

      "/etc/bind/zones/db.b18steni_192_rev.tmpl.mustache"
	create => "true",
	edit_defaults => empty,
	edit_line => b18steni_192_rev_zon;

      "/etc/bind/zones/db.b18steni_192_rev"
	create => "true",
	edit_defaults => empty,
	template_method => "mustache",
	edit_template => "/etc/bind/zones/db.b18steni_192_rev.tmpl.mustache",
	classes => results("bundle", "bind9_zone_data"),
	meta => { "bind" };

      "$(prefix)$(controlled_files)"              perms => mog("444", "root", "bind");
      "/etc/bind/zones/db.b18steni.tmpl.mustache" perms => mog("444", "root", "bind");
      "/etc/bind/zones/db.b18steni"               perms => mog("444", "root", "bind");
      "/etc/bind/zones/db.b18steni_192_rev"       perms => mog("444", "root", "bind");

  packages:
    DNS_server::
      "bind9"
	policy => "present",
	package_module => apt_get,
	classes => results("bundle", "bind9_installed"),
	meta => { "bind" };

  processes:
      ".*named"
	process_select => named_selector,
	restart_class => "bind9_proc_missing";

  services:
    DNS_server::
      "bind9"
	ifvarclass => "bind9_proc_missing|bind9_installed_repaired",
	service_policy => "enable";

      "bind9"
	ifvarclass => "bind9_proc_missing|bind9_config_repaired",
	service_policy => "restart";

  commands:
    DNS_server::
      "/usr/sbin/rndc"
	args => "reload",
	ifvarclass => "bind9_zone_data_repaired",
	meta => {"bind"};

  reports:
    verbose_mode|DEBUG|default:DEBUG::
      "$(this.bundle): Hello, this is an automatically loaded bundle";

    ns01.verbose_mode|ns01.DEBUG|ns01.default:DEBUG::
      "$(this.bundle): named är osynlig" ifvarclass => "bind9_proc_missing";
      "$(this.bundle): named är igång"   unless => "bind9_proc_missing";
}

body process_select named_selector
{
	command => "named";
	process_owner => {"bind"};
	process_result => "process_owner.command";
}

bundle edit_line named_conf
{
  insert_lines:
      "//
// avsedd för {{vars.sys.fqhost}}
//
// cfe-regler från {{vars.sys.policy_hub}}
// senast uppdatering av policy: {{sys.last_policy_update}}
//
// This is the primary configuration file for the BIND DNS server named.
//
// Please read /usr/share/doc/bind9/README.Debian.gz for information on the
// structure of BIND configuration files in Debian, *BEFORE* you customize
// this configuration file.
//
// If you are just adding zones, please do that in /etc/bind/named.conf.local
include \"/etc/bind/named.conf.options\";
include \"/etc/bind/named.conf.local\";
include \"/etc/bind/named.conf.default-zones\";
";
}

bundle edit_line named_conf_local
{
  insert_lines:
      "//
// avsedd för {{vars.sys.fqhost}}
//
// cfe-regler från {{vars.sys.policy_hub}}
// senast uppdatering av policy: {{sys.last_policy_update}}
//

zone \"b18steni.it119g.nsa.his.se\" {
type master;
file \"/etc/bind/zones/db.b18steni\";
};

zone \"168.192.in-addr.arpa\" {
type master;
file \"/etc/bind/zones/db.b18steni_192_rev\";
};
";
}

bundle edit_line named_conf_options
{
  insert_lines:
      "//
// avsedd för {{vars.sys.fqhost}}
//
// cfe-regler från {{vars.sys.policy_hub}}
// senast uppdatering av policy: {{sys.last_policy_update}}
//

acl my_computers { 192.168.0.0/16; 10.204.12.0/24; 10.204.20.0/24; };

options {
    directory \"/var/cache/bind\";
    dnssec-validation auto;

    forwarders {
	   10.0.252.201;
	   10.0.252.202;
    };

    listen-on-v6 { any; };
    auth-nxdomain no;    # conform to RFC1035

    allow-recursion { my_computers; };
    allow-query { any; };
};";
}

bundle edit_line b18steni_zon
{
  insert_lines:
      "
$TTL 10
@ IN SOA ns01 stefan.{{vars.def.domain}}. (
    2019012901 ; serial
    30m       ;
    3m ;
    2w ;
    1h )
	IN NS ns01

{{#vars.bind.template_data.dns_resolvable_hosts}}
{{key}}  IN  {{type}}  {{data}}
{{/vars.bind.template_data.dns_resolvable_hosts}}
";
}

bundle edit_line b18steni_192_rev_zon
{
  insert_lines:
      "
$TTL 10
@ IN SOA ns01 stefan.{{vars.def.domain}}. (
    2019012904 ; serial
    30m       ;
    3m ;
    2w ;
    1h )
	IN NS ns01.b18steni.it119g.nsa.his.se.

11.1 IN PTR pc01.b18steni.it119g.nsa.his.se.
12.1 IN PTR pc02.b18steni.it119g.nsa.his.se.

21.1 IN PTR dhcp01.b18steni.it119g.nsa.his.se.

21.2 IN PTR webserver01.b18steni.it119g.nsa.his.se.
22.2 IN PTR ns01.b18steni.it119g.nsa.his.se.
24.2 IN PTR cfengine.b18steni.it119g.nsa.his.se.
25.2 IN PTR vcs.b18steni.it119g.nsa.his.se.

";
}

# Local Variables:
# mode:cfengine
# End:
