#
# -*-cfengine-*-
#
# Time-stamp: <2019-03-29 10:14:17 nsa>
#

bundle agent dhcp
{
  meta:
      "tags" slist => { "autorun" };

  vars:
    any::
      "template_data"
	   data => readjson("$(this.promise_dirname)/hosts.json");

  files:
    DHCP_server::
      #
      # spara undan dist-versionen av filerna
      #
      "/etc/default/isc-dhcp-server.saved_dist_version"
	 unless => fileexists("/etc/default/isc-dhcp-server.saved_dist_version"),
      copy_from => local_dcp("/etc/default/isc-dhcp-server"),
	comment => "save dist versionen";

      "/etc/dhcp/dhcpd.conf.saved_dist_version"
	 unless => fileexists("/etc/dhcp/dhcpd.conf.saved_dist_version"),
      copy_from => local_dcp("/etc/dhcp/dhcpd.conf"),
	comment => "save dist versionen";

      #
      # etc/default/isc-dhcp-server
      #
      "/etc/default/isc-dhcp-server.tmpl.mustache"
	 create => "true",
  edit_defaults => empty,
      edit_line => default_isc_dhcp_server;

      "/etc/default/isc-dhcp-server"
	 create => "true",
  edit_defaults => empty,
template_method => "mustache",
  edit_template => "/etc/default/isc-dhcp-server.tmpl.mustache";

      "/etc/default/isc-dhcp-server.tmpl.mustache" perms => m("444");
      "/etc/default/isc-dhcp-server"               perms => m("444");

      #
      # etcd/dhcp/dhcpd.conf
      #
      "$(template_data[dhcpd_conf_file]).tmpl.mustache"
	 create => "true",
  edit_defaults => empty,
      edit_line => dhcpd_conf;

      "$(template_data[dhcpd_conf_file])"
	 create => "true",
  edit_defaults => empty,
template_method => "mustache",
  edit_template => "$(template_data[dhcpd_conf_file]).tmpl.mustache",
	classes => results("bundle", "dhcpd_conf_changed");

      "$(template_data[dhcpd_conf_file]).tmpl.mustache" perms => m("444");
      "$(template_data[dhcpd_conf_file])"               perms => m("444");

  packages:
    DHCP_server::
      "isc-dhcp-server"
	 policy => "present",
	classes => results("namespace", "isc_dhcp_server_installed"),
 package_module => apt_get,
	   meta => { "dhcp" };

  services:
    DHCP_server::
      "isc-dhcp-server"
     ifvarclass => "isc_dhcp_server_installed_repaired",
 service_policy => "enable";

      "isc-dhcp-server"
 service_policy => "restart",
     ifvarclass => "dhcpd_conf_changed_repaired";

  reports:
    verbose_mode|DEBUG|DEBUG_b18steni::
      "$(this.bundle): Hello, this is an automatically loaded bundle";
      "$(this.bundle): isc-dhcpd:s configuration ändrades"
     ifvarclass => "dhcpd_conf_changed_repaired";
}

bundle edit_line dhcpd_conf
{
  insert_lines:
      "#
# avsedd för {{vars.sys.fqhost}}
#
# def.json tidsstämpel: {{{vars.def.timestamp}}}
# host.json tidsstämpel: {{{vars.dhcp.template_data.timestamp}}}
# senast uppdatering av policy: {{sys.last_policy_update}}
#
# cfe-regler från {{vars.sys.policy_hub}}
#

ddns-update-style none;

option routers 192.168.1.1;
option domain-name \"{{vars.def.domain}}\";
option domain-name-servers {{vars.dhcp.template_data.dns_nameserver}};
option ntp-servers 194.58.203.20;

default-lease-time 600;
max-lease-time 7200;

# If this DHCP server is the official DHCP server for the local
# network, the authoritative directive should be uncommented.
authoritative;

# Use this to send dhcp log messages to a different log file (you also
# have to hack syslog.conf to complete the redirection).
log-facility local7;

subnet 192.168.1.0 netmask 255.255.255.0 {
  range 192.168.1.65 192.168.1.126;
  option routers 192.168.1.1;
}

{{#vars.dhcp.template_data.mac_mappings}}
host {{name}}.{{vars.def.domain}} {
     hardware ethernet {{mac_addr}};
     fixed-address {{ip_addr}};
}
{{/vars.dhcp.template_data.mac_mappings}}
";
}

bundle edit_line default_isc_dhcp_server
{
  insert_lines:
      "#
# avsedd för {{vars.sys.fqhost}}
#
# def.json tidsstämpel: {{{vars.def.timestamp}}}
# host.json tidsstämpel: {{{vars.dhcp.template_data.timestamp}}}
# senast uppdatering av policy: {{vars.sys.last_policy_update}}
#
# cfe-regler från {{vars.sys.policy_hub}}
#
# Defaults for isc-dhcp-server initscript
# sourced by /etc/init.d/isc-dhcp-server
# installed at /etc/default/isc-dhcp-server by the maintainer scripts

# DHCPD_CONF={{vars.dhcp.template_data.dhcpd_conf_file}}
# DHCPD_PID={{vars.dhcp.template_data.dhcpd_pid_file}}
# DHCPD_LEASE_FILE={{vars.dhcp.template_data.dhcpd_lease_file}}
OPTIONS={{vars.dhcp.template_data.dhcpd_options}}
INTERFACES={{vars.dhcp.template_data.dhcpd_listen_iface}}
";
}

# Local Variables:
# mode:cfengine
# cfengine-parameters-indent: (promise arrow 10)
# End:
